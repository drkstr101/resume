/**
 * @author Aaron R Miller
 */

plugins {
    id 'base'
    id 'eclipse'
    id 'idea'
    id 'project-report'
    id 'drkstr101.resume'
}

group = 'drkstr101.resume'
version = '1.3.1'

ext {
    distDir = file('public')
    templateName = 'altacv'
}

document {
    inputDirectory = file("template/${templateName}")
}

resume {
    fullName = 'Aaron R Miller'
    email = 'drkstr101@gmail.com'
}

apply from: file('data/skills.groovy')
apply from: file('data/references.groovy')
apply from: file('data/accomplishments.groovy')
apply from: file('data/employers.groovy')

tasks.register('publishPlugin') {
    dependsOn gradle.includedBuild('gradle-resume').task(':publish')
}

tasks.register('publishSkillCloud', Copy) {
    from skillCloud
    into distDir
    rename '(.+)\\.(.+)', "${project.group}.\$1-${project.version}.\$2"
    mustRunAfter skillCloud, clean
}

tasks.register('publish') {
    group = 'Publishing'
    dependsOn build, publishPlugin, publishSkillCloud
}

tasks.register('copyArtifact', Copy) {
    from(document.outputDirectory.file('main.pdf')) {
        rename 'main.pdf', "${project.group}-${project.version}.pdf"
    }
    into(file('public'))
}

build {
    finalizedBy copyArtifact
}
